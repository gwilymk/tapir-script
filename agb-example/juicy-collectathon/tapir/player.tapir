property anim_frame: int;
property position: fix2;

global ACCEL = 0.3;
global DECEL = 0.7;

global MAX_SPEED = 2.;

global current_speed = fix2(0, 0);

global blink_task: task;
global is_stunned = false;

fn blink_task() {
    anim_frame = 0;

    loop {
        wait 100;
        anim_frame = 1;
        wait;
        anim_frame = 2;
        wait 10;
        anim_frame = 1;
        wait;
        anim_frame = 0;
    }
}
 
blink_task = spawn blink_task();

event fn input(x: fix, y: fix) {
    if is_stunned {
        return;
    }

    var dir = fix2(x, y);
    current_speed = current_speed + dir * ACCEL;

    if dir.x * current_speed.x <= 0. {
        current_speed.x = current_speed.x * DECEL;
    }

    if dir.y * current_speed.y <= 0. {
        current_speed.y = current_speed.y * DECEL;
    }

    current_speed.x = current_speed.x.clamp(-MAX_SPEED, MAX_SPEED);
    current_speed.y = current_speed.y.clamp(-MAX_SPEED, MAX_SPEED);

    if current_speed.x.abs() < 0.1 { current_speed.x = 0.; }
    if current_speed.y.abs() < 0.1 { current_speed.y = 0.; }

    position = position + current_speed;

    position.x = position.x.clamp(0., 240. - 16.);
    position.y = position.y.clamp(0., 160. - 16.);
}

event fn hurt() {
    if is_stunned {
        return;
    }

    is_stunned = true;
    trigger PlaySound(4);

    current_speed = fix2(0., 0.);
    blink_task.cancel();
    anim_frame = 3;
    
    var count = 10;
    loop {
        if count <= 0 {
            break;
        }

        count = count - 1;
        position.x = position.x + 1.;
        wait;
        position.x = position.x - 1.;
        wait;
        position.x = position.x - 1.;
        wait;
        position.x = position.x + 1.;
        wait;
    }

    is_stunned = false;
    blink_task = spawn blink_task();

    trigger PlayerDamaged;
}

event fn die() {
    is_stunned = true;
    blink_task.cancel();
    current_speed = fix2(0., 0.);
    anim_frame = 3;

    var count = 200;
    loop {
        if count <= 0 {
            break;
        }
        count = count - 1;

        position.y = position.y - 0.3;
        wait;

        var pos = position.round();

        if count % 60 == 0 {
            trigger SpawnParticle(pos.x + 8, pos.y + 8, 2);
            trigger SpawnParticle(pos.x + 8, pos.y + 8, 2);
            trigger SpawnParticle(pos.x + 8, pos.y + 8, 2);
            trigger SpawnParticle(pos.x + 8, pos.y + 8, 2);
            trigger SpawnParticle(pos.x + 8, pos.y + 8, 2);
            trigger SpawnParticle(pos.x + 8, pos.y + 8, 2);
            trigger SpawnParticle(pos.x + 8, pos.y + 8, 2);
            trigger PlaySound(2);

            trigger ScreenShake(10);
        }
    }

    trigger PlayerDied;
    trigger PlaySound(2);
    trigger PlaySound(2);
}