property anim_frame: int;
property position: fix2;
property should_show: bool;

global animate: task;
global urgency: task;

animate = spawn animate_spin();
urgency = spawn flash();

global animate_wait = 3;

fn animate_spin() {
    loop {
        anim_frame = anim_frame + 1;
        wait animate_wait;
    }
}

global has_been_collided = false;
global is_health_coin = false;

event fn become_health_coin() {
    is_health_coin = true;
}

event fn collide_with_player() {
    if has_been_collided {
        return;
    }
    has_been_collided = true;

    trigger IncreaseScore;

    urgency.cancel();

    # Wait one frame to allow main.rs to tell us if we're a health coin
    wait 1;

    should_show = true;
    animate_wait = 1;
    var count = 16;
    loop {
        if count <= 0 {
            break;
        }

        position.y = position.y - 1.;
        wait 3;
        count = count - 1;
    }

    if is_health_coin {
        trigger CoinToHealth(position.round());
        trigger DestroySelf;
        return;
    }

    var particle_position = position.round() + int2(8, 8);

    trigger SpawnParticle(particle_position, 0);
    trigger SpawnParticle(particle_position, 0);
    trigger SpawnParticle(particle_position, 0);
    trigger SpawnParticle(particle_position, 0);
    trigger SpawnParticle(particle_position, 0);

    trigger ScreenShake(3);
    trigger PlaySound(5);

    trigger DestroySelf;
}

fn flash() {
    wait 5 * 60; # give 5 seconds before it starts flashing

    var num_flashes = 10;
    var flash_wait = 15;
    loop {
        should_show = true;
        wait flash_wait;
        should_show = false;
        wait flash_wait;

        num_flashes = num_flashes - 1;
        flash_wait = flash_wait - 1;
        if num_flashes <= 0 {
            break;
        }
    }

    trigger DestroySelf;
}