use crate::{
    tokens::{
        FileId, LexicalError, Token, Span,
    },
    ast::*,
    reporting::{Diagnostic, DiagnosticMessage, Diagnostics, ErrorKind},
    types::Type,
};

use num_traits::Num;

grammar<'input>(file_id: FileId, diagnostics: &mut Diagnostics);

extern {
    type Location = usize;
    type Error = LexicalError;

    enum Token<'input> {
        wait => Token::KeywordWait,
        var => Token::KeywordVar,
        "if" => Token::KeywordIf,
        "extern" => Token::KeywordExtern,
        "else" => Token::KeywordElse,
        "fn" => Token::KeywordFn,
        t_integer => Token::KeywordInt,
        t_fix => Token::KeywordFix,
        t_bool => Token::KeywordBool,
        return => Token::KeywordReturn,
        spawn => Token::KeywordSpawn,
        loop => Token::KeywordLoop,
        break => Token::KeywordBreak,
        continue => Token::KeywordContinue,
        event => Token::KeywordEvent,
        then => Token::KeywordThen,
        trigger => Token::KeywordTrigger,
        global => Token::KeywordGlobal,
        property => Token::KeywordProperty,
        builtin => Token::KeywordBuiltin,
        "struct" => Token::KeywordStruct,

        identifier => Token::Identifier(<&'input str>),
        integer => Token::Integer(<&'input str>),
        fix => Token::Fix(<&'input str>),

        true => Token::True,
        false => Token::False,

        "{" => Token::LBrace,
        "}" => Token::RBrace,
        "(" => Token::LParen,
        ")" => Token::RParen,
        ";" => Token::Semicolon,
        "=" => Token::Assign,
        ":" => Token::Colon,
        "->" => Token::Arrow,
        "," => Token::Comma,

        "+" => Token::OperatorAdd,
        "-" => Token::OperatorSub,
        "*" => Token::OperatorMul,
        "/" => Token::OperatorDiv,
        "%" => Token::OperatorMod,
        "//" => Token::OperatorRealDiv,
        "%%" => Token::OperatorRealMod,
        "==" => Token::OperatorEqEq,
        "!=" => Token::OperatorNeEq,
        ">" =>  Token::OperatorGt,
        ">=" => Token::OperatorGtEq,
        "<" =>  Token::OperatorLt,
        "<=" => Token::OperatorLtEq,
        "||" => Token::OperatorOr,
        "&&" => Token::OperatorAnd,
        "." => Token::Dot,
    }
}

pub Script: Script<'input> = <TopLevelStatement*> => Script::from_top_level(<>, file_id);

TopLevelStatement: TopLevelStatement<'input> = {
    <Statement> => TopLevelStatement::Statement(<>),
    <FunctionDefinition> => TopLevelStatement::FunctionDefinition(<>),
    <ExternFunctionDefinition> => TopLevelStatement::ExternFunctionDefinition(<>),
    <BuiltinFunctionDecl> => TopLevelStatement::BuiltinFunctionDefinition(<>),
    <GlobalDecl> => TopLevelStatement::GlobalDeclaration(<>),
    <PropertyDecl> => TopLevelStatement::PropertyDeclaration(<>),
    <StructDecl> => TopLevelStatement::StructDeclaration(<>),
    <error: !> => {
        diagnostics.add_lalrpop(<>.error, file_id);
        TopLevelStatement::Error
    },
}

GlobalDecl: GlobalDeclaration<'input> = {
    <start:@L> global <name:TypedIdentifier> "=" <value:Expression> ";" <end:@R> =>
        GlobalDeclaration { name, value, span: Span::new(file_id, start, end) },
}

PropertyDecl: PropertyDeclaration<'input> = {
    <start:@L> property <name:TypedIdentifierRequired> ";" <end:@R> =>
        PropertyDeclaration { name, span: Span::new(file_id, start, end) },
}

StructDecl: StructDeclaration<'input> = {
    <start:@L> "struct" <name:Identifier> "{" <fields:CommaSeparated<TypedIdentifierRequired>> "}" <end:@R> =>
        StructDeclaration { name, fields, span: Span::new(file_id, start, end) },
}

BuiltinFunctionDecl: BuiltinFunction<'input> = {
    builtin "(" <builtin_id: BuiltinId> ")" "fn" <start: @L> <name: identifier> <end: @R> "(" <arguments: FunctionArguments> ")" <return_type: FunctionReturn> ";" =>
        BuiltinFunction { name, builtin_id, arguments, return_type, span: Span::new(file_id, start, end), meta: Metadata::new() },
}

BuiltinId: BuiltinFunctionId = {
    <value: integer> => match value.parse::<i16>() {
        Ok(id) => BuiltinFunctionId(id),
        Err(_) => BuiltinFunctionId(0), // Error handling could be improved
    },
}

FunctionDefinition: Function<'input> = {
    <modifiers: FunctionModifiers> "fn" <start: @L> <name: identifier> <end: @R> "(" <arguments: FunctionArguments> ")" <return_types: FunctionReturn> <statements: Block> =>
        Function { name, arguments, return_types, statements, span: Span::new(file_id, start, end), modifiers, meta: Metadata::new() },
}

ExternFunctionDefinition: ExternFunctionDefinition<'input> = {
    "extern" "fn" <start: @L> <name: identifier> <end: @R> "(" <arguments: FunctionArguments> ")" <return_types: FunctionReturn> ";" =>
        ExternFunctionDefinition { name, arguments, return_types, span: Span::new(file_id, start, end), meta: Metadata::new() },
    
    "extern" "fn" <start: @L> <name: identifier> <end: @R> "(" <arguments: FunctionArguments> ")" <return_types: FunctionReturn> <block_start: @L> Block <block_end: @R> => {
        let span = Span::new(file_id, block_start, block_end);
        ErrorKind::ExternFunctionWithBlock { name: name.to_string() }
            .at(span)
            .label(span, DiagnosticMessage::ExternFunctionCannotHaveBody)
            .emit(diagnostics);
        ExternFunctionDefinition { name, arguments, return_types, span: Span::new(file_id, start, end), meta: Metadata::new() }
    }
}

FunctionModifiers: FunctionModifiers = {
    () => FunctionModifiers::default(),
    <event_start: @L> event <event_end: @R> => FunctionModifiers { is_event_handler: Some(Span::new(file_id, event_start, event_end)) },
}
FunctionArguments: Vec<TypedIdent<'input>> = CommaSeparated<TypedIdentifierRequired>;

// Required type annotation (for function args, properties)
TypedIdentifierRequired: TypedIdent<'input> = {
    <ident: Identifier> ":" <ty: Type> =>
        TypedIdent {
            ident,
            ty: Some(ty),
        },
};
FunctionReturn: FunctionReturn<'input> = {
    <VoidReturn>,
    "->" <start: @L> <t: Type> <end: @R> => FunctionReturn { types: vec![t], span: Span::new(file_id, start, end) },
    "->" "(" <start: @L> <types: CommaSeparated<Type>> <end: @R> ")" => FunctionReturn { types, span: Span::new(file_id, start, end) },
}
VoidReturn: FunctionReturn<'input> = <start: @L> <end: @R> => FunctionReturn { types: vec![], span: Span::new(file_id, start, end) };

CommaSeparated<T>: Vec<T> =
    <mut v:(<T> ",")*> <e:T?> => match e {
        None => v,
        Some(e) => {
            v.push(e);
            v
        },
    };


Type: TypeWithLocation<'input> =
    <start: @L> <type_and_name: TypeWithoutLocation> <end: @R> => TypeWithLocation {
        t: type_and_name.0,
        name: type_and_name.1,
        span: Span::new(file_id, start, end)
    };
TypeWithoutLocation: (Option<Type>, &'input str) = {
    t_integer => (Some(Type::Int), "int"),
    t_bool => (Some(Type::Bool), "bool"),
    t_fix => (Some(Type::Fix), "fix"),
    // User-defined types (structs) are parsed as identifiers with t=None,
    // then resolved to Some(Type::Struct(id)) by struct_visitor during compilation.
    <ident: identifier> => (None, ident),
}

Statement: Statement<'input> =
    <start: @L> <kind: StatementKind> <end: @R> => kind.with_span(file_id, start, end);

StatementKind: StatementKind<'input> = {
    var <idents: CommaSeparated<TypedIdentifier>> "=" <values: CommaSeparated<Expression>> ";" => StatementKind::VariableDeclaration { <> },
    <idents: CommaSeparated<Identifier>> "=" <values: CommaSeparated<Expression>> ";" => StatementKind::Assignment { <> },
    wait ";" => StatementKind::Wait,
    break ";" => StatementKind::Break,
    continue ";" => StatementKind::Continue,

    "if" <condition: Expression> <true_block: Block> <false_block: ElseBlock?> => StatementKind::If { 
        condition, true_block, false_block: false_block.unwrap_or_else(Vec::new),
    },
    loop <block: Block> => StatementKind::Loop { <> },

    <name: identifier> "(" <arguments: CommaSeparated<Expression>> ")" ";" => StatementKind::Call { <> },
    return <CommaSeparated<Expression>> ";" => StatementKind::Return { values: <> },

    spawn <name: identifier> "(" <arguments: CommaSeparated<Expression>> ")" ";" => StatementKind::Spawn { <> },
    trigger <name: identifier> "(" <arguments: CommaSeparated<Expression>> ")" ";" => StatementKind::Trigger { <> },
    trigger <name: identifier> ";" => StatementKind::Trigger { name, arguments: vec![] },

    <block: Block> => StatementKind::Block { <> },
    
    <error: !> ";" => {
        diagnostics.add_lalrpop(<>.error, file_id);
        StatementKind::Error
    }
}

Identifier: Ident<'input> =
    <start: @L> <ident: identifier> <end: @R> => Ident { ident, span: Span::new(file_id, start, end) };

// Optional type annotation (for var, global)
TypedIdentifier: TypedIdent<'input> = {
    <ident: Identifier> =>
        TypedIdent {
            ident,
            ty: None,
        },
    <ident: Identifier> ":" <ty: Type> =>
        TypedIdent {
            ident,
            ty: Some(ty),
        },
};

Block: Vec<Statement<'input>> = "{" <Statement*> "}";
ElseBlock = "else" <Block>;

Expression: Expression<'input> = {
    #[precedence(level="1")]
    <PostfixExpr>,

    #[precedence(level="2")]
    #[assoc(side="left")]
    <start: @L> <lhs: Expression> <operator: MulDiv> <rhs: Expression> <end: @R> => 
        ExpressionKind::BinaryOperation { lhs: Box::new(lhs), operator, rhs: Box::new(rhs) }.with_span(file_id, start, end),

    #[precedence(level="3")]
    #[assoc(side="left")]
    <start: @L> <lhs: Expression> <operator: AddSub> <rhs: Expression> <end: @R> => 
        ExpressionKind::BinaryOperation { lhs: Box::new(lhs), operator, rhs: Box::new(rhs) }.with_span(file_id, start, end),

    #[precedence(level="4")]
    #[assoc(side="none")]
    <start: @L> <lhs: Expression> <operator: ComparisonOp> <rhs: Expression> <end: @R> =>
        ExpressionKind::BinaryOperation { lhs: Box::new(lhs), operator, rhs: Box::new(rhs) }.with_span(file_id, start, end),

    #[precedence(level="5")]
    #[assoc(side="left")]
    <start: @L> <lhs: Expression> "&&" <rhs: Expression> <end: @R> =>
        ExpressionKind::BinaryOperation { lhs: Box::new(lhs), operator: BinaryOperator::And, rhs: Box::new(rhs) }.with_span(file_id, start, end),

    #[precedence(level="6")]
    #[assoc(side="left")]
    <start: @L> <lhs: Expression> "||" <rhs: Expression> <end: @R> =>
        ExpressionKind::BinaryOperation { lhs: Box::new(lhs), operator: BinaryOperator::Or, rhs: Box::new(rhs) }.with_span(file_id, start, end),

    #[precedence(level="7")]
    #[assoc(side="none")]
    <start: @L> <lhs: Expression> <operator: then> <rhs: Expression> <end: @R> =>
        ExpressionKind::BinaryOperation { lhs: Box::new(lhs), operator: BinaryOperator::Then, rhs: Box::new(rhs) }.with_span(file_id, start, end),
}

MulDiv: BinaryOperator = {
    "*" => BinaryOperator::Mul,
    "/" => BinaryOperator::Div,
    "//" => BinaryOperator::RealDiv,
    "%%" => BinaryOperator::RealMod,
    "%" => BinaryOperator::Mod,
}

AddSub: BinaryOperator = {
    "+" => BinaryOperator::Add,
    "-" => BinaryOperator::Sub,
}

ComparisonOp: BinaryOperator = {
    "==" => BinaryOperator::EqEq,
    "!=" => BinaryOperator::NeEq,
    ">" => BinaryOperator::Gt,
    ">=" => BinaryOperator::GtEq,
    "<" => BinaryOperator::Lt,
    "<=" => BinaryOperator::LtEq,
}

PostfixExpr: Expression<'input> = {
    <Term>,
    <start:@L> <base:PostfixExpr> "." <field:Identifier> <end:@R> =>
        ExpressionKind::FieldAccess { base: Box::new(base), field }.with_span(file_id, start, end),
}

Term: Expression<'input> = {
    "(" <Expression> ")",
    <Num>,
    <start: @L> <ident: identifier> <end: @R> => ExpressionKind::Variable(ident).with_span(file_id, start, end),
    <start: @L> <b: Bool> <end: @R> => ExpressionKind::Bool(b).with_span(file_id, start, end),
    <start: @L> <error: !> <end: @R> => {
        diagnostics.add(Diagnostic::from_lalrpop(error.error, file_id));
        ExpressionKind::Error.with_span(file_id, start, end)
    },

    <start: @L> <ident: identifier> "(" <arguments: CommaSeparated<Expression>> ")" <end: @R> => 
        ExpressionKind::Call {
            name: ident,
            arguments,
        }.with_span(file_id, start, end),
}

Num: Expression<'input> = {
    <start: @L> <value: integer> <end: @R> => match value.parse::<i32>() {
        Ok(value) => ExpressionKind::Integer(value).with_span(file_id, start, end),
        Err(e) => {
            let span = Span::new(file_id, start, end);
            ErrorKind::InvalidNumber { error: e }
                .at(span)
                .label(span, DiagnosticMessage::InvalidInteger)
                .emit(diagnostics);
            ExpressionKind::Error.with_span(file_id, start, end)
        }
    },
    <start: @L> <value: fix> <end: @R> => match Fix::from_str_radix(value, 10) {
        Ok(value) => ExpressionKind::Fix(value).with_span(file_id, start, end),
        Err(_e) => {
            let span = Span::new(file_id, start, end);
            ErrorKind::InvalidFix
                .at(span)
                .label(span, DiagnosticMessage::InvalidFixnumLabel)
                .emit(diagnostics);
            ExpressionKind::Error.with_span(file_id, start, end)
        }
    }
}

Bool: bool = {
    true => true,
    false => false,
}